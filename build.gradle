buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'fml'

ext.set("revision", getRevision())
ext.set("build", getBuildNo())
version = "1.11.1." + revision
group = "acs.tabbychat"
archivesBaseName = "TabbyChat"

minecraft{
    version = "1.7.2-7.2.217.935"
    runDir = "eclipse"

	setExt "liteloaderVersion", "1.7.2_04"
	
	replace "@@VERSION@@", project.version
}
processResources{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
	inputs.property "revision", project.build
	
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info', 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'revision':project.build, 'llversion':project.minecraft.getExt("liteloaderVersion")
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info', 'litemod.json'
    }
}
configurations{
	customCompile
}
dependencies{
	customCompile 'net.sf.jazzy:jazzy:0.5.2-rtext-1.4.1-2'
	compile configurations.customCompile
}
jar{
	from sourceSets.api.output
	from ( configurations.customCompile.collect { it.isDirectory() ? it : zipTree(it)}) { exclude 'META-INF/**' }
	appendix minecraft.version
	exclude 'acs/tabbychat/core/LiteModTabbyChat.class', 'litemod.json'
}
task jarDev(type: Jar){
	from sourceSets.main.output
	from sourceSets.api.output
	appendix minecraft.version
	classifier "dev"
}
task jarSrc(type: Jar){
	from sourceSets.main.allSource
	from sourceSets.api.allSource
	appendix = minecraft.version
	classifier "src"
}
artifacts{
	archives jarDev
	archives jarSrc
}
String getBuildNo(){
	return git("rev-list", "HEAD", "--count")
}
String getRevision(){
	String tag = git("describe", "--tags", "--abbrev=0")
	return git("rev-list", tag + "..HEAD", "--count")
}
import static net.minecraftforge.gradle.dev.DevBasePlugin.*
String git(String[] args){
	try{
		return runGit(project, project.getProjectDir(), args)
	}catch(Exception e){
		return null
	}
}
// Because there's no mcp jar for liteloader-1.7.2_05
apply plugin: 'liteloader'
jarLiteloader{
	from sourceSets.api.output
	from (configurations.customCompile.collect { it.isDirectory() ? it : project.zipTree(it)}) { exclude 'META-INF/**' }
	appendix minecraft.version
	exclude 'acs/tabbychat/core/TabbyChatMod.class', 'mcmod.info'
}